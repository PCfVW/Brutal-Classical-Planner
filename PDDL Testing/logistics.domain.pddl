(define (domain logistics-strips)
  (:predicates (OBJ ?o)
	       (TRUCK ?t)
	       (at ?o ?l)
	       (in ?o ?t)
	       (AIRPLANE ?p)
	       (AIRPORT ?s)
	       (IN-CITY ?s ?city)
	       (CITY ?c)
	       (LOCATION ?l))
  ;; Need separate loading operations for trucks and planes, but
  ;; single unloading operation will do.
  (:action load-truck
	     :parameters (?o ?truck ?loc)
	     :precondition (and (OBJ ?o)
				 (TRUCK ?truck)
				 (at ?o ?loc)
				 (at ?truck ?loc))
	     :effect (and (not (at ?o ?loc))
			   (in ?o ?truck)))
  (:action load-plane
	     :parameters (?o ?p ?loc)
	     :precondition (and (OBJ ?o)
				 (AIRPLANE ?p)
				 (at ?o ?loc)
				 (at ?p ?loc))
	     :effect (and (not (at ?o ?loc))
			   (in ?o ?p)))
  (:action unload
	     :parameters (?o ?v ?loc)
	     :precondition (and (in ?o ?v)
				 (at ?v ?loc))
	     :effect (and (at ?o ?loc)
			   (not (in ?o ?v))))
  (:action fly
	     :parameters (?p ?s ?d)
	     :precondition (and (AIRPLANE ?p)
				 (AIRPORT ?s)
				 (AIRPORT ?d)
				 (at ?p ?s))
	     :effect (and (at ?p ?d)
			   (not (at ?p ?s))))
  (:action drive
	     :parameters (?truck ?s ?d ?city)
	     :precondition (and (TRUCK ?truck)
				 (at ?truck ?s)
				 (IN-CITY ?s ?city)
				 (IN-CITY ?d ?city))
	     :effect (and (at ?truck ?d)
			   (not (at ?truck ?s)))))

